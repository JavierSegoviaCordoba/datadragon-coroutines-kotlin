plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.21'
    id "com.jfrog.bintray" version "1.8.4"
    id "maven-publish"
}

group 'DataDragonCoroutinesKotlin'
version '1.0'

repositories {
    jcenter()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
}

dependencies {
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    api 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.2.0-alpha'
    api "com.squareup.retrofit2:retrofit:2.5.1-SNAPSHOT"
    api "com.squareup.retrofit2:converter-gson:2.5.1-SNAPSHOT"
    api 'com.squareup.retrofit2:converter-jackson:2.5.1-SNAPSHOT'
    api "com.fasterxml.jackson.module:jackson-module-kotlin:2.9.7"
    api 'org.koin:koin-core:2.0.0-rc-2'
    testApi "org.jetbrains.kotlin:kotlin-test"
    testApi "org.jetbrains.kotlin:kotlin-test-junit"
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

// Bintray
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    publish = true

    pkg {
        repo = 'datadragon-coroutines-kotlin'
        name = group
        userOrg = user
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/JavierSegoviaCordoba/datadragon-coroutines-kotlin'
        version {
            name = project.version
        }
    }
    publications = ['Publication']
}

publishing {
    publications {
        Publication(MavenPublication) {
            from components.java
            artifact sourcesJar
            groupId 'com.javiersc'
            artifactId project.name
            version project.version
        }
    }
}

